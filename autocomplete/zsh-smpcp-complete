#compdef smpcp

#
# .▄▄ · • ▌ ▄ ·.  ▄▄▄· ▄▄·  ▄▄▄· super
# ▐█ ▀. ·██ ▐███▪▐█ ▄█▐█ ▌▪▐█ ▄█ music
# ▄▀▀▀█▄▐█ ▌▐▌▐█· ██▀·██ ▄▄ ██▀· player
# ▐█▄▪▐███ ██▌▐█▌▐█▪·•▐███▌▐█▪·• client
#  ▀▀▀▀ ▀▀  █▪▀▀▀.▀   ·▀▀▀ .▀    plus+
#
# This file is part of smpcp.
# Copyright (C) 2021, Stéphane MEYER.
#
# Smpcp is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <https://www.gnu.org/licenses/>
#
# SMPCP-AUTOCOMPLETE
# C : 2021/05/31
# M : 2021/06/03
# D : Zsh completion for smpcp.

_smpcp_args() {
  local args=($*)
  _arguments '*:args:($args)'
}

_smpcp() {
  local commands
  commands=(${(f)"$(smpcp help | awk '/^ *smpcp [a-z]+ /{print $2}')"})
  
  if (( CURRENT == 2 )); then
    _describe -t commands "smpcp commands" commands
  elif (( CURRENT == 3 )); then
    local cur="$words[3]"
    case $words[2] in
      add | lsdir | update )
        local directories
        directories=(${(f)"$(smpcp lsdir "$cur" 2> /dev/null)"})
        _arguments '*:directories:($directories)'
      ;;
      addalbum | albums | insertalbum | lsalbums | playalbum )
        local artists
        artists=(${(f)"$(smpcp lsartists 2> /dev/null)"})
        _arguments '*:artists:($artists)'
      ;;
      consume | random | repeat | single )
        _smpcp_args "on" "off"
      ;;
      cload | load | pls )
        local playlists
        playlists=(${(f)"$(smpcp pls 2> /dev/null)"})
        _arguments '*:playlists:($playlists)'
      ;;
      mode )
        _smpcp_args "album" "normal" "song"
      ;;
      replaygain )
        _smpcp_args "album" "auto" "track"
      ;;
    esac
  elif (( CURRENT == 4 )); then
    case $words[2] in
      addalbum | insertalbum | playalbum )
        local cur="$words[3]"
        local albums
        albums=(${(f)"$(smpcp lsalbums "$cur" 2> /dev/null)"})
        _arguments '*:albums:($albums)'
      ;;
    esac
  fi

  return
}

_smpcp
